apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'hu.rxd'

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/groups/staging'
	}
//	maven {
//		url 'http://maven.jenkins-ci.org:8081/content/repositories/releases'
//	}
}

dependencies {
   compile gradleApi()
   compile localGroovy()
   
   compile('org.codehaus.groovy.modules.http-builder:http-builder:0.5.2') {
	   exclude(module: 'groovy')
   }
   
   compile 'xmlunit:xmlunit:1.3'
   compile('hu.rxd:job-dsl-core:1.20-rxd-+'){
	exclude(module: 'groovy')
   exclude module: 'slf4j-simple'

   }
	testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'

}

task wrapper(type: Wrapper) {
	gradleVersion = '1.9'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  description = 'An archive of the GroovyDocs for Maven Central'
  classifier = 'javadoc'
  from groovydoc
}
 
task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives groovydocJar
	archives sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signPom(deployment) }
 
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
			  authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
 
			pom.project {
			   name 'gradle-jenkins-dsl'
			   packaging 'jar'
			   description 'This is a gradle plugin for programmatically configuring Jenkins jobs, using javaposse dsl dialect'
			   url 'https://github.com/kgyrtkirk/gradle-jenkins-dsl'
 
			   scm {
				   url 'https://github.com/kgyrtkirk/gradle-jenkins-dsl.git'
				   connection 'scm:https://github.com/kgyrtkirk/gradle-jenkins-dsl.git'
				   developerConnection 'scm:git@github.com/kgyrtkirk/gradle-jenkins-dsl.git'
			   }
 
			   licenses {
				   license {
					   name 'The Apache Software License, Version 2.0'
					   url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					   distribution 'repo'
				   }
			   }
 
			   developers {
				   developer {
					   id 'kirk'
					   name 'Zoltan Nagy'
					   email 'kirk@rxd.hu'
				   }
				   developer {
					   id 'ghale'
					   name 'Gary Hale'
					   email 'ghhale@computer.org'
				   }
			   }
		   }
		}
	}
}

